dnl libopenraw configure
dnl written by Hubert Figuiere

AC_PREREQ(2.50)
AC_INIT(include/libopenraw/libopenraw.h)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(libopenraw, 0.0.1)
AM_MAINTAINER_MODE


dnl ---------------------------------------------------------------------------
dnl Advanced information about versioning:
dnl   * "Writing shared libraries" by Mike Hearn
dnl         http://navi.cx/~mike/writing-shared-libraries.html
dnl   * libtool.info chapter "Versioning"
dnl   * libtool.info chapter "Updating library version information"
dnl ---------------------------------------------------------------------------
dnl Versioning:
dnl  - CURRENT (Major):  Increment if the interface has changes. AGE is always
dnl                      *changed* at the same time.
dnl  - AGE (Micro):      Increment if any interfaces have been added; set to 0
dnl                      if any interfaces have been removed. Removal has
dnl                      precedence over adding, so set to 0 if both happened.
dnl                      It denotes upward compatibility.
dnl  - REVISION (Minor): Increment any time the source changes; set to
dnl                      0 if you incremented CURRENT.
dnl
dnl  To summarize. Any interface *change* increment CURRENT. If that interface
dnl  change does not break upward compatibility (ie it is an addition),
dnl  increment AGE, Otherwise AGE is reset to 0. If CURRENT has changed,
dnl  REVISION is set to 0, otherwise REVISION is incremented.
dnl ---------------------------------------------------------------------------
AC_SUBST([LIBOPENRAW_REVISION], [0])
AC_SUBST([LIBOPENRAW_AGE],      [0])
AC_SUBST([LIBOPENRAW_CURRENT],  [0])
AC_SUBST([LIBOPENRAW_CURRENT_MIN],
         [`expr $LIBOPENRAW_CURRENT - $LIBOPENRAW_AGE`])
AC_SUBST([LIBOPENRAW_VERSION_INFO],
         [$LIBOPENRAW_CURRENT:$LIBOPENRAW_REVISION:$LIBOPENRAW_AGE])

dnl the major version. used for package names, etc
LIBOPENRAW_MAJOR_VERSION=$LIBOPENRAW_CURRENT_MIN.0
AC_SUBST(LIBOPENRAW_MAJOR_VERSION)

dnl the path where includes are installed
LIBOPENRAW_INCLUDE_BASE=libopenraw-$LIBOPENRAW_MAJOR_VERSION
AC_SUBST(LIBOPENRAW_INCLUDE_BASE)


dnl --------------------------------------------------------------------
dnl guess directory to install *.pc into
dnl --------------------------------------------------------------------
pkgconfigdir='${libdir}/pkgconfig'
AC_SUBST(pkgconfigdir)


AM_CXXFLAGS="-ansi -pedantic -g -Wall -Werror  -Weffc++"
AM_CFLAGS="-pedantic -g -Wall"

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LIBTOOL


dnl Requirements

dnl Needed for GdkPixbuf
LIBGLIB_REQUIRED=2.0.0
dnl Optional GdkPixbuf support
LIBGDKPIXBUF_REQUIRED=2.0.0

AC_SUBST(LIBGLIB_REQUIRED)
AC_SUBST(LIBGDKPIXBUF_REQUIRED)

AC_ARG_WITH(fink,[  --with-fink          add /sw/... to CPP/LDFLAGS (Mac OSX)],[
        if test "x$withval" != "xno"; then
                CPPFLAGS="$CPPFLAGS -I/sw/include"
                LDFLAGS="$LDFLAGS -L/sw/lib"
        fi
])

AC_LANG(C++)
AC_CHECK_HEADER(boost/shared_ptr.hpp, ,
	AC_MSG_ERROR([boost headers not found]))

AC_LANG(C)
AC_CHECK_HEADER(jpeglib.h, ,
	AC_MSG_ERROR([jpeglib headers not found]))


#
# Checks for libglib-2.0
#
HAVE_LIBGLIB=no
PKG_CHECK_MODULES(LIBGLIB, glib-2.0 >= $LIBGLIB_REQUIRED,
                    [AC_DEFINE(HAVE_LIBGLIB, 1, [Define to 1 to enable libglib support])
                     HAVE_LIBGLIB=yes],
                    [HAVE_LIBGLIB=no])

#
# Checks for libgdkpixbuf
#
AC_ARG_ENABLE([libgdkpixbuf],	
              [AC_HELP_STRING([--disable-libgdkpixbuf],[disable support for libgdkpixbuf])],,
              [enable_libgdkpixbuf=yes])
HAVE_LIBGDKPIXBUF=no
if test x$enable_libgdkpixbuf = xyes ; then
   if test x$HAVE_LIBGLIB = xyes ; then
      PKG_CHECK_MODULES(LIBGDKPIXBUF, gdk-pixbuf-2.0 >= $LIBGDKPIXBUF_REQUIRED,
                     [AC_DEFINE(HAVE_LIBGDKPIXBUF, 1, [Define to 1 to enable libgdkpixbuf support])
                      HAVE_LIBGDKPIXBUF=yes],
                     [HAVE_LIBGDKPIXBUF=no])
   else
      AC_MSG_WARN([glib-2.0 missing, ignore GdkPixbuf support.])
   fi
fi


AC_OUTPUT([
Makefile
include/Makefile
include/libopenraw/Makefile
include/libopenraw++/Makefile
lib/Makefile
lib/capi/Makefile
lib/io/Makefile
lib/libopenraw-1.0.pc
gnome/Makefile
gnome/libopenraw-gnome-1.0.pc
doc/Makefile
doc/Doxyfile
doc/doxygen/Makefile
test/Makefile
demo/Makefile
])


echo "
Options:

  GdkPixbuf support:     ${HAVE_LIBGDKPIXBUF}
"